; Basic test to check timer was implemented correctly.

__START:
_la $6 main ; Vector 0: "Reset"
JPR $6 0
NOP

HLT         ; Vector 1: ---
HLT
HLT
HLT

HLT         ; Vector 2: ---
HLT
HLT
HLT

HLT         ; Vector 3: ---
HLT
HLT
HLT

HLT         ; Vector 4: ---
HLT
HLT
HLT

HLT         ; Vector 5: ---
HLT
HLT
HLT

HLT         ; Vector 6: ---
HLT
HLT
HLT

_la $4 tmr1 ; Vector 8: TMR1
JPR $4 0
HLT

_la $4 tmr0 ; Vector 8: TMR0
JPR $4 0
HLT

HLT         ; Vector 9: ---
HLT
HLT
HLT

HLT         ; Vector 10: ---
HLT
HLT
HLT

HLT			; Vector 11: ---
HLT
HLT
HLT

; Registers
; R0 = 0x8008 (NVIC)
; R1 = 0x8018 (GPIO)
; R2 = 0x8020 (TMR0)
; R3 = 0x0100 (NVIC and GPIO mask)
; R4 = Intermediate
; R5 = Intermediate
; R6 = 0x0080 (NVIC mask for TMR1)


tmr0:
; Toggle GPIO
LDR $4 $1 3		; Read GPIO output
XOR $4 $4 -1	; Flip it all
STR $4 $1 3		; Write flipped output

; Clear flag
LDR $4 $0 1		; Load current flags
XOR $5 $3 -1	; clear mask for flag
AND $4 $4 $5	; Clear flag from flags
STR $4 $0 1		; Write flags back to NVIC

; RTI
JPR %r

tmr1:
; Decrement TMR0's max count
LDR $4 $2 2		; Read TMR0_MAX
LBI    $5 0x4  	; Decrement by 0xffff / 32 = 0x0400
LBI %s $5 0x00
SUB $4 $5 $4	; Decrement
STR $4 $2 2		; Write TMR0_MAX

; Clear flag
LDR $4 $0 1		; Load current flags
XOR $5 $6 -1	; clear mask for flag
AND $4 $4 $5	; Clear flag from flags
STR $4 $0 1		; Write flags back to NVIC

; RTI
JPR %r


main:
; Setup registers.
LBI		$0	-128	; R0 = 0x8008
LBI %s	$0	0x08
LBI		$1	-128	; R1 = 0x8018
LBI %s	$1	0x18
LBI		$2	-128	; R2 = 0x8020
LBI %s	$2	0x20
LBI		$3	0x01	; R3 = 0x0100
LBI %s	$3	0x00
LBI		$6  0		; R6 = 0x0080
LBI %s  $6  -128

; Clear WDT.
LBI $4 0x01		; Load "cancel" mask
STR $4 $0 4		; Write to WDT_CTRL

; Prep GPIO writing.
STR $3 $1 2		; Write to direction register- pin 8 is now an output

; Check settings
HLT

; Enable interrupt (after clearing flags).
LBI $4 0
STR $4 $0 1		; Cleared flags
ORR $4 $3 $6	; TMR0 and TMR1 enables
STR $4 $0 0		; Enable TMR0/TMR1 interrupts

; Turn on timer 0 w/ full range count + full prescale
LBI $4 -1
STR $4 $2 2		; Max = 0xffff
STR $4 $2 0		; Prescale = 0xf + enabled

; Turn on timer 1 w/ full range count + full prescale
LBI $4 -1
STR $4 $2 6		; Max = 0xffff
STR $4 $2 4		; Prescale = 0xf + enabled

; (Spin)
BRC %nzp -1

; Safety Halt
HLT